FUNCTION "FNV1a_32bit" : DWord
TITLE = FNV1-a - Hashing Function
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : kurcontko
FAMILY : Hash4PLC
VERSION : 0.1
//Implementation of the FNV-1a hash function for 32-bit hashing. Suitable for fast hash generation with a good distribution characteristic.
   VAR_INPUT 
      str : String;   // Input string to be hashed
   END_VAR

   VAR_TEMP 
      h : DWord;   // Running hash value
      i : Int;   // Loop counter for string iteration
      c : Byte;   // Individual character value from the input string
   END_VAR

   VAR CONSTANT 
      SEED : DWord := 16#811C9DC5;   // Initial seed value for FNV-1a
      FNV_PRIME : DWord := 16#01000193;   // FNV-1a prime number
   END_VAR


BEGIN
	//    FNV-1a Hash Function:
	//    ---------------------
	//    - A non-cryptographic hash function known for its fast performance and low collision rate.
	//    - Produces a 32-bit hash value for a given string input.
	//    - The algorithm is based on iteratively applying XOR and multiplication operations over each byte of the string.
	
	#h := #SEED;
	
	FOR #i := 1 TO LEN(#str) DO
	    
	    #c := #str[#i];
	    
	    // XOR current hash value with character byte
	    #h := #h XOR #c;
	    
	    // Multiply hash with the specific prime value
	    #h := #h * #FNV_PRIME; 
	
	END_FOR;
	
	#FNV1a_32bit := #h;
END_FUNCTION

